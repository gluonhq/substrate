plugins {
    id 'java'
    id 'maven-publish'
}

group = "com.gluonhq"
version = "0.0.1-SNAPSHOT"

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        stackTraceFilters = []
    }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$jUnitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"
}

javadoc {
    options.addStringOption("Xdoclint:none", "-quiet")
    options.addBooleanOption('html5', true)
    options.addBooleanOption('javafx', true)
}

build.dependsOn javadoc

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'substrate'
            version = project.version
        }
    }
    repositories {
        maven {
            def repositoryUrl = project.findProperty('repositoryUrl') ?: 'https://nexus.gluonhq.com/nexus/content/repositories/public-snapshots/'
            if (project.hasProperty('gluonNexusUsername') && project.hasProperty('gluonNexusPassword')) {
                url repositoryUrl
                credentials {
                    username project.property('gluonNexusUsername')
                    password project.property('gluonNexusPassword')
                }
            }
        }
    }
}