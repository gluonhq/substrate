plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
}

def attachVersion = "4.0.5"
def target = System.getProperty("substrate.target")
def classifier = "ios" == target ? target : "desktop"

configurations {
    substrate
}

dependencies {
    substrate "com.gluonhq:substrate:$substrateVersion"

    implementation "com.gluonhq:charm-glisten:6.0.2"
    implementation "com.gluonhq.attach:display:$attachVersion:$classifier"
    implementation "com.gluonhq.attach:lifecycle:$attachVersion:$classifier"
    if ("ios" == target) {
        implementation "com.gluonhq.attach:statusbar:$attachVersion:$classifier"
    } else {
        implementation "com.gluonhq.attach:statusbar:$attachVersion"
    }
    implementation "com.gluonhq.attach:storage:$attachVersion:$classifier"
    implementation "com.gluonhq.attach:util:$attachVersion"
}

javafx {
    version = "13.0.1"
    modules = [ "javafx.controls" ]
}

mainClassName = "com.gluonhq.substrate.test.Main"

task runScript(type: Exec) {
    dependsOn 'build'

    def javaHome = validateJavaHome()
    def graalvmHome = validateGraalvmHome()
    def javafxStaticSdk = validateJavafxStaticSdk()

    def argsList = ["${javaHome}/bin/java", '-cp', "${project.configurations.substrate.asPath}",
                    "-Dimagecp=${javafxStaticSdk}/lib/javafx.base.jar:${javafxStaticSdk}/lib/javafx.graphics.jar:${javafxStaticSdk}/lib/javafx.controls.jar:" +
                            "${project.sourceSets.main.runtimeClasspath.filter{!it.name.contains('javafx')}.asPath}",
                    "-Djavafxsdk=${javafxStaticSdk}",
                    "-Dgraalvm=${graalvmHome}"]
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    } else {
        argsList += ["-Dprism.sw=true", "-Dprism.order=sw"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
