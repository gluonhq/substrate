plugins {
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
}

def attachVersion = "4.0.10"
def target = System.getProperty("substrate.target")
def classifier = "ios" == target ? target : "desktop"

configurations {
    substrate
}

dependencies {
    substrate "com.gluonhq:substrate:$substrateVersion"

    implementation "com.gluonhq:charm-glisten:6.0.6"
    implementation "com.gluonhq.attach:display:$attachVersion:$classifier"
    implementation "com.gluonhq.attach:lifecycle:$attachVersion:$classifier"
    if ("ios" == target) {
        implementation "com.gluonhq.attach:statusbar:$attachVersion:$classifier"
    } else {
        implementation "com.gluonhq.attach:statusbar:$attachVersion"
    }
    implementation "com.gluonhq.attach:storage:$attachVersion:$classifier"
    implementation "com.gluonhq.attach:util:$attachVersion"
}

javafx {
    version = "16"
    modules = [ "javafx.controls" ]
}

mainClassName = "com.gluonhq.substrate.test.Main"

task runScript(type: Exec) {
    dependsOn 'build'

    def javaHome = validateJavaHome()
    def graalvmHome = validateGraalvmHome()
    def javafxStaticSdk = validateJavafxStaticSdk()
    def consoleProcessLog = System.getProperty("consoleProcessLog")

    def imageClasspath = ""
    if (javafxStaticSdk != null) {
        imageClasspath = "${javafxStaticSdk}/lib/javafx.base.jar:" +
                "${javafxStaticSdk}/lib/javafx.graphics.jar:" +
                "${javafxStaticSdk}/lib/javafx.controls.jar:" +
                project.sourceSets.main.runtimeClasspath.filter{!it.name.contains('javafx')}.asPath
    } else {
        imageClasspath = project.sourceSets.main.runtimeClasspath.asPath
    }

    def argsList = ["${javaHome}/bin/java", '-cp', project.configurations.substrate.asPath,
                    "-Dimagecp=${imageClasspath}",
                    "-Dgraalvm=${graalvmHome}"]
    if (javafxStaticSdk != null) {
        argsList += "-Djavafxsdk=${javafxStaticSdk}"
    }
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    } else {
        argsList += ["-Dprism.sw=true"]
    }
    if (consoleProcessLog != null) {
        argsList += ["-DconsoleProcessLog=$consoleProcessLog"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
