plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
}

def attachVersion = "4.0.2"
def target = System.getProperty("substrate.target")
def classifier = "ios" == target ? target : "desktop"

dependencies {
    implementation "com.gluonhq:charm-glisten:6.0.1"
    implementation "com.gluonhq.attach:display:$attachVersion:$classifier"
    implementation "com.gluonhq.attach:lifecycle:$attachVersion:$classifier"
    if ("ios" == target) {
        implementation "com.gluonhq.attach:statusbar:$attachVersion:$classifier"
    } else {
        implementation "com.gluonhq.attach:statusbar:$attachVersion"
    }
    implementation "com.gluonhq.attach:storage:$attachVersion:$classifier"
    implementation "com.gluonhq.attach:util:$attachVersion"

    if ("ios" == target) {
        implementation 'com.googlecode.plist:dd-plist:1.22'
        implementation 'com.github.jnr:jnr-ffi:2.1.10'
        implementation 'org.bouncycastle:bcpkix-jdk15on:1.49'
    }
}

javafx {
    version = "13.0.1"
    modules = [ "javafx.controls" ]
}

mainClassName = "com.gluonhq.substrate.test.Main"

def java_home = hasProperty('org.gradle.java.home') ? getProperty('org.gradle.java.home') : System.getenv('JAVA_HOME')

task runScript(type: Exec) {
    dependsOn 'build'

    def graal_path = setupGraalDependencies(platform)

    if (java_home == null) {
        throw new RuntimeException("java_home is not defined.")
    }

    def javafx_path = setupJavaFXDependencies(platform, target)

    def rootFile = new File(rootDir.getParent().concat("/build/"))
    def substratePath = rootFile.toString().concat("/classes/java/main") + ":" +
            rootFile.toString().concat("/resources/main") + ":" +
            "${project.sourceSets.main.runtimeClasspath.filter{!it.name.contains('javafx')}.asPath}"

    def argsList = ["${java_home}/bin/java", '-cp', substratePath,
                    "-Dimagecp=${javafx_path}/javafx.base.jar:${javafx_path}/javafx.graphics.jar:${javafx_path}/javafx.controls.jar:" +
                            "${project.sourceSets.main.runtimeClasspath.filter{!it.name.contains('javafx')}.asPath}",
                    "-Dgraalvm=$graal_path",
                    "-Dreflectionlist=com.gluonhq.impl.charm.glisten.control.skin.GlistenButtonSkin"]
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    } else {
        argsList += ["-Dprism.sw=true"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
