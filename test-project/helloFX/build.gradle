plugins {
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def target = System.getProperty("substrate.target")

configurations {
    substrate
}

dependencies {
    substrate "com.gluonhq:substrate:$substrateVersion"
}

javafx {
    version = "15.0.1"
    modules = [ "javafx.controls" ]
}

mainClassName = "com.gluonhq.substrate.test.Main"

task runScript(type: Exec) {
    dependsOn 'build'

    def javaHome = validateJavaHome()
    def graalvmHome = validateGraalvmHome()
    def javafxStaticSdk = validateJavafxStaticSdk()

    def expected = System.getProperty("expected")
    def consoleProcessLog = System.getProperty("consoleProcessLog")

    def imageClasspath = project.sourceSets.main.runtimeClasspath.filter{!it.name.contains('javafx')}.asPath
    if (javafxStaticSdk != null) {
        imageClasspath = "${javafxStaticSdk}/lib/javafx.base.jar:${javafxStaticSdk}/lib/javafx.graphics.jar:${javafxStaticSdk}/lib/javafx.controls.jar:" + imageClasspath
    }

    def argsList = ["${javaHome}/bin/java", '-cp', project.configurations.substrate.asPath,
                    "-Dimagecp=${imageClasspath}",
                    "-Dgraalvm=${graalvmHome}"]
    if (javafxStaticSdk != null) {
        argsList += "-Djavafxsdk=${javafxStaticSdk}"
    }
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    } else {
        argsList += ["-Dprism.sw=true"]
    }
    if (expected != null) {
        argsList += ["-Dexpected=$expected"]
    }
    if (consoleProcessLog != null) {
        argsList += ["-DconsoleProcessLog=$consoleProcessLog"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
