plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    mavenCentral()
}

javafx {
    version = "13.0.1"
    modules = [ "javafx.controls" ]
}

run {
    jvmArgs = ["-Dprism.verbose=true", "-Dprism.order=sw"]
}

mainClassName = "com.gluonhq.substrate.test.Main"

def java_home = hasProperty('org.gradle.java.home') ? getProperty('org.gradle.java.home') : System.getenv('JAVA_HOME')

task runScript(type: Exec) {
    dependsOn 'clean'
    dependsOn 'build'

    def graal_path = setupDependencies(platform)

    if (java_home == null) {
        throw new RuntimeException("java_home is not defined.")
    }

    def rootFile = new File(rootDir.getParent().concat("/build/"))
    def substratePath = rootFile.toString().concat("/classes/java/main") + ":" + rootFile.toString().concat("/resources/main")
    def expected = System.getProperty("expected")

    def argsList = ["${java_home}/bin/java", '-cp', substratePath,
                     "-Dimagecp=${project.sourceSets.main.runtimeClasspath.asPath}",
                     "-Dgraalvm=$graal_path", "-Djavafx=true", "-Dprism.sw=true"]
    if (expected != null) {
        argsList += ["-Dexpected=$expected"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
