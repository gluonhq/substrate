plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def target = System.getProperty("substrate.target")

configurations {
    substrate
}

dependencies {
    substrate "com.gluonhq:substrate:$substrateVersion"
}

javafx {
    version = "13.0.1"
    modules = [ "javafx.controls" ]
}

mainClassName = "com.gluonhq.substrate.test.Main"

def java_home = hasProperty('org.gradle.java.home') ? getProperty('org.gradle.java.home') : System.getenv('JAVA_HOME')

task runScript(type: Exec) {
    dependsOn 'build'

    if (java_home == null) {
        throw new RuntimeException("java_home is not defined.")
    }

    def graalvmPath = getGraalVMPath()
    def javafxPath = setupJavaFXDependencies(platform, target)

    def expected = System.getProperty("expected")

    def argsList = ["${java_home}/bin/java", '-cp', "${project.configurations.substrate.asPath}",
                    "-Dimagecp=${javafxPath}/javafx.base.jar:${javafxPath}/javafx.graphics.jar:${javafxPath}/javafx.controls.jar:" +
                            "${project.sourceSets.main.runtimeClasspath.filter{!it.name.contains('javafx')}.asPath}",
                    "-Dgraalvm=$graalvmPath"]
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    } else {
        argsList += ["-Dprism.sw=true"]
    }
    if (expected != null) {
        argsList += ["-Dexpected=$expected"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
