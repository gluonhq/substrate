plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def target = System.getProperty("substrate.target")

configurations {
    substrate
}

dependencies {
    substrate "com.gluonhq:substrate:$substrateVersion"
}

javafx {
    version = "13.0.1"
    modules = [ "javafx.controls", "javafx.fxml" ]
}

mainClassName = "com.gluonhq.substrate.test.Main"

task runScript(type: Exec) {
    dependsOn 'build'

    def javaHome = validateJavaHome()
    def graalvmHome = validateGraalvmHome()
    def javafxStaticLibs = validateJavafxStaticLibs()

    def expected = System.getProperty("expected")

    def argsList = ["${javaHome}/bin/java", '-cp', "${project.configurations.substrate.asPath}",
                    "-Dimagecp=${javafxStaticLibs}/javafx.base.jar:${javafxStaticLibs}/javafx.graphics.jar:${javafxStaticLibs}/javafx.controls.jar:${javafxStaticLibs}/javafx.fxml.jar:" +
                            "${project.sourceSets.main.runtimeClasspath.filter{!it.name.contains('javafx')}.asPath}",
                    "-Dgraalvm=${graalvmHome}",
                    "-Dbundleslist=com.gluonhq.substrate.test.bundle",
                    "-Dreflectionlist=javafx.fxml.FXMLLoader,com.gluonhq.substrate.test.MainController,javafx.scene.control.Button,javafx.scene.control.Label",
            ]
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    } else {
        argsList += ["-Dprism.sw=true"]
    }
    if (expected != null) {
        argsList += ["-Dexpected=$expected"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
