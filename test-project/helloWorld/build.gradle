
repositories {
    mavenCentral()
}

def target = System.getProperty("substrate.target")

dependencies {
    if ("ios" == target) {
        implementation 'com.googlecode.plist:dd-plist:1.22'
        implementation 'com.github.jnr:jnr-ffi:2.1.10'
        implementation 'org.bouncycastle:bcpkix-jdk15on:1.49'
    }
}

mainClassName = "com.gluonhq.substrate.test.Main"

def java_home = hasProperty('org.gradle.java.home') ? getProperty('org.gradle.java.home') : System.getenv('JAVA_HOME')

task runScript(type: Exec) {
    dependsOn 'clean'
    dependsOn 'build'

    def graal_path = setupGraalDependencies(platform)

    if (java_home == null) {
        throw new RuntimeException("java_home is not defined.")
    }

    def skipsigning = System.getProperty("skipsigning")
    def expected = System.getProperty("expected")

    def rootFile = new File(rootDir.getParent().concat("/build/"))
    def substratePath = rootFile.toString().concat("/classes/java/main") + ":" +
            rootFile.toString().concat("/resources/main") + ":" +
            "${project.sourceSets.main.runtimeClasspath.asPath}"

    def argsList = ["${java_home}/bin/java", '-cp', substratePath,
                   "-Dimagecp=${project.sourceSets.main.runtimeClasspath.asPath}",
                   "-Dgraalvm=$graal_path"]
    if (target != null) {
        argsList += ["-DtargetProfile=$target"]
    }
    if (skipsigning != null) {
        argsList += ["-Dskipsigning=$skipsigning"]
    }
    if (expected != null) {
        argsList += ["-Dexpected=$expected"]
    }
    argsList += ["-Dmainclass=$mainClassName", 'com.gluonhq.substrate.SubstrateDispatcher']
    commandLine argsList
}
